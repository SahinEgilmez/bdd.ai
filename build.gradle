plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.sahinegilmez'
version = '0.1.0'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    compile group: 'io.appium', name: 'java-client', version: '7.2.0'
    compile group: 'io.cucumber', name: 'cucumber-core', version: '4.7.2'
    compile group: 'io.cucumber', name: 'cucumber-java', version: '4.7.2'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
}

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'io.github.sahinegilmez'
            artifactId 'bdd.ai'
            version '0.1.0'

            from components.java

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Mobile testing automation library with artificial intelligence support'
            name 'bdd.ai'
            url 'https://github.com/SahinEgilmez/bdd.ai'
            organization {
                name 'io.github.sahinegilmez'
                url 'https://github.com/SahinEgilmez'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/SahinEgilmez/bdd.ai/issues'
            }
            /**
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://github.com/mautini/schemaorg-java/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            **/
            scm {
                url 'https://github.com/SahinEgilmez/bdd.ai'
                connection 'scm:git:git://github.com/SahinEgilmez/bdd.ai.git'
                developerConnection 'scm:git:ssh://git@github.com:SahinEgilmez/bdd.ai.git'
            }
            developers {
                developer {
                    name 'Sahin Egilmez'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}
